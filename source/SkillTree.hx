package;

import flixel.FlxG;
import flixel.FlxSprite;
import flixel.group.FlxGroup.FlxTypedGroup;
import flixel.text.FlxText;

/**
	To make this skill tree reusable, you would need to create an external data source out of structs or JSON data or whatever else.
	Then you could pass that data into the skill tree constructor, and then loop over that data to create nodes and tooltips out of it.
**/
class SkillTree
{
	// In a bigger game these should probably be made regular variables, and the SkillTree class should be a singleton, otherwise you
	// have a bunch of global static variables that aren't being cleaned up. Unless I intend to make the skill tree available at all times.
	public static var nodes = new FlxTypedGroup<SkillTreeNode>();
	public static var tooltips = new FlxTypedGroup<Tooltip>();

	public static var availablePoints(default, set) = 0;
	public static var totalPointsSpent = -1;

	static var availablePointsLabel:FlxText;

	// TODO: Create prerequisite talents by adding a field that references another skill, and check if that skill has been unlocked. You
	// can do this by referencing another node, or by storing the name of a skill and searching the nodes group for that skill.

	public function new()
	{
		FlxG.state.add(nodes);
		FlxG.state.add(tooltips);

		availablePointsLabel = new FlxText(0, 0, 200, "", 16);
		availablePointsLabel.font = "Roboto";
		availablePointsLabel.x = FlxG.width - availablePointsLabel.frameWidth - 20;
		availablePointsLabel.y = 20;
		availablePoints = 51;
		FlxG.state.add(availablePointsLabel);

		nodes.add(new SkillTreeNode(0, 0, 3, 0, new Tooltip("Improved Heroic Strike", "Reduces the cost of your Heroic Strike ability by 1 rage point.")));
		nodes.add(new SkillTreeNode(0, 1, 5, 0, new Tooltip("Deflection", "Increases your parry chance by 1%.")));
		nodes.add(new SkillTreeNode(0, 2, 3, 0, new Tooltip("Improved Rend", "Increases the bleed damage done by your Rend ability by 15%.")));

		nodes.add(new SkillTreeNode(1, 0, 2, 5, new Tooltip("Improved Charge", "Increases the amount of rage generated by your Charge ability by 3.")));
		nodes.add(new SkillTreeNode(1, 1, 5, 5, new Tooltip("Tactical Mastery", "You retain up to 5 of your rage points when you change stances.")));
		nodes.add(new SkillTreeNode(1, 3, 3, 5, new Tooltip("Improved Thunder Clap", "Reduces the rage cost of your Thunder Clap ability by 1 rage point.")));

		nodes.add(new SkillTreeNode(2, 0, 2, 10, new Tooltip("Improved Overpower", "Increases the critical strike chance of your Overpower ability by 25%.")));
		nodes.add(new SkillTreeNode(2, 1, 1, 10,
			new Tooltip("Anger Management", "Increases the time required for your rage to decay while out of combat by 30%.")));
		nodes.add(new SkillTreeNode(2, 2, 3, 10,
			new Tooltip("Deep Wounds", "Your critical strikes cause the opponent to bleed, dealing 20% of your melee weapon's average damage over 12 sec.")));

		nodes.add(new SkillTreeNode(3, 1, 5, 15,
			new Tooltip("Two-Handed Weapon Specialization", "Increases the damage you deal with two-handed weapons by 1%.")));
		nodes.add(new SkillTreeNode(3, 2, 2, 15,
			new Tooltip("Impale", "Increases the critical strike damage bonus of your abilities in Battle, Defensive, and Berserker stance by 10%.")));

		nodes.add(new SkillTreeNode(4, 0, 5, 20, new Tooltip("Axe Specialization", "Increases your chance to get a critical strike with Axes by 1%.")));
		nodes.add(new SkillTreeNode(4, 1, 1, 20, new Tooltip("Sweeping Strikes", "Your next 5 melee attacks strike an additional nearby opponent.")));
		nodes.add(new SkillTreeNode(4, 2, 5, 20, new Tooltip("Mace Specialization", "Gives you a 1% chance to stun your target for 3 sec with a Mace.")));
		nodes.add(new SkillTreeNode(4, 3, 5, 20,
			new Tooltip("Sword Specialization", "Gives you a 1% chance to get an extra attack on the same target after dealing damage with your Sword.")));

		nodes.add(new SkillTreeNode(5, 0, 5, 25,
			new Tooltip("Polearm Specialization", "Increases your chance to get a critical strike with Polearms by 1%.")));
		nodes.add(new SkillTreeNode(5, 2, 3, 25,
			new Tooltip("Improved Hamstring", "Gives your Hamstring ability a 5% chance to immobilize the target for 5 sec.")));

		nodes.add(new SkillTreeNode(6, 1, 1, 30,
			new Tooltip("Mortal Strike",
				"A vicious strike that deals weapon damage plus 85 and wounds the target, reducing the effectiveness of any healing by 50% for 10 sec.")));

		repositionNodes();
	}

	/**
		Uses the row and column indices of each node to position them in a grid.
	**/
	function repositionNodes()
	{
		for (node in nodes.members)
		{
			node.button.x = 30 + (node.columnIndex * 60);
			node.button.y = 30 + (node.rowIndex * 60);
			node.pointsLabel.x = node.button.x;
			node.pointsLabel.y = node.button.y + node.button.frameHeight;
		}
	}

	static function set_availablePoints(value)
	{
		availablePoints = value;
		availablePointsLabel.text = 'Available Points: $availablePoints';
		totalPointsSpent++;
		return value;
	}
}
